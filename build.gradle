buildscript {
	ext {
		springBootVersion = '2.0.0.M7'
		springCloudSkipperVersion = '1.0.0.BUILD-SNAPSHOT'
		junitPlatformVersion = '1.0.2'
		junitJupiterVersion = '5.0.2'
		dockerComposeRuleJunit4Version = '0.33.0'
		gradleDockerVersion = '0.17.2'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://dl.bintray.com/palantir/releases" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.junit.platform:junit-platform-gradle-plugin:${junitPlatformVersion}")
		classpath("com.palantir.gradle.docker:gradle-docker:${gradleDockerVersion}")
	}
}

def getResolvedVersionOf(configurations, dependency) {
	return configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.findAll{
		it.moduleName == dependency
		}.moduleArtifacts.first()
}

configure(allprojects) {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'io.spring.dependency-management'

	group = 'org.springframework.cloud.skipper.acceptance'

	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://dl.bintray.com/palantir/releases" }
		mavenLocal()
		maven {
			url "http://repo.springsource.org/ext-private-local"
			credentials {
				username = System.getenv('ARTIFACTORY_USERNAME') ?: artifactoryUsername
				password = System.getenv('ARTIFACTORY_PASSWORD') ?: artifactoryPassword
			}
		}
	}

	compileJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}
	
	dependencyManagement {
		imports {
		}
		dependencies {
			dependency "com.palantir.docker.compose:docker-compose-rule-junit4:$dockerComposeRuleJunit4Version"
			dependency "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
			dependency "org.springframework.cloud:spring-cloud-skipper-server:$springCloudSkipperVersion"
		}
	}
}

configure(subprojects) { subproject ->
	apply plugin: 'org.springframework.boot'

	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}
}

project('spring-cloud-skipper-acceptance-core') {
	apply plugin: 'org.junit.platform.gradle.plugin'
	dependencies {
		compile('com.palantir.docker.compose:docker-compose-rule-junit4')
		compile('org.junit.jupiter:junit-jupiter-api')
		compile('org.junit.jupiter:junit-jupiter-engine')
		compile('org.junit.jupiter:junit-jupiter-params')
		testCompile('org.springframework.boot:spring-boot-starter-test')
	}
}

project('spring-cloud-skipper-acceptance-docker') {
	apply plugin: 'com.palantir.docker'

	dependencies {
		compile('org.springframework.cloud:spring-cloud-skipper-server')
	}

	docker {
		name "spring-cloud-skipper-acceptance-docker"
		dockerfile file('src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations, "spring-cloud-skipper-server").file.absolutePath[0], 'src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations, "spring-cloud-skipper-server").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}
	
	build.dependsOn 'docker'
	
}

project('spring-cloud-skipper-acceptance-docker2') {
	apply plugin: 'com.palantir.docker'

	dependencies {
		compile('org.springframework.cloud.skipper.acceptance.app:skipper-oracle:1.0.0.BUILD-SNAPSHOT')
	}

	docker {
		name "spring-cloud-skipper-acceptance-docker2"
		dockerfile file('src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations, "skipper-oracle").file.absolutePath[0], 'src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations, "skipper-oracle").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}
	
	build.dependsOn 'docker'
	
}

project('spring-cloud-skipper-acceptance-tests') {
	apply plugin: 'org.junit.platform.gradle.plugin'

	dependencies {
		compile project(":spring-cloud-skipper-acceptance-core")
		compile project(":spring-cloud-skipper-acceptance-docker")
		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile('org.springframework.boot:spring-boot-starter-web')
	}

		
	build.dependsOn ':spring-cloud-skipper-acceptance-docker:docker'
}






